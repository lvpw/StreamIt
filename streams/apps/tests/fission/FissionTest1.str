/**
* This doesn't actually test fission, but it tests the output of
* fission.  It turns out that at one point, this output executed
* correctly on the Raw backend, but not on the cluster backend.
*/

void->void pipeline FissionTest1 {
    add IntSource;
    add FIRFiss;
    add IntPrinter;
}

void->int stateful filter IntSource {
  int i = 0;
  work push 1 {
    push(i++);
  }
}

int->int splitjoin FIRFiss {
    split duplicate;
    add FIR1;
    add FIR2;
    join roundrobin(2, 1);
}

int->void filter IntPrinter {
    work pop 1 {
        println(pop());
    }
}

int->int filter FIR1 {
    work pop 3 push 2 {
        for (int i=0; i<2; i++) {
            int sum = 0;
            for (int j=0; j<2; j++) {
                sum += j * peek(j);
            }
            push(sum);
            pop();
        }
        pop();
    }
}

int->int filter FIR2 {
    work pop 3 peek 4 push 1 {
        for (int i=0; i<2; i++) {
            pop();
        }
        int sum = 0;
        for (int j=0; j<2; j++) {
            sum += j * peek(j);
        }
        push(sum);
        pop();
    }
}
