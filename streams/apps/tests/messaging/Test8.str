 /**
 * A test of messaging with dynamic rates and phases.
 *
 * Correct output:
 *
 * strc -library -i 3 Test8.str
 *
 * got the message 0
 * 0
 * 1
 * 2
 * 3
 * got the message 1
 * 7
 * 6
 * 5
 * 4
 * got the message 2
 * 11
 * 10
 * 9
 * 8
 */
void->void pipeline Test8 {
   portal<SomeReceiver> thePortal;

   add void->int filter {
       int count = 0;
       work push 4 {
           thePortal.getTheMessage(count % 3);
           for (int i = 0; i < 4; i++) {
               push(count++);
           }
       }
   }

   add SomeReceiver() to thePortal;
}

int->void filter Printer() {
    work pop 1 {
        println("value: " + pop());
    }
}

int->void filter SomeReceiver  {
   int message_value;
   int[4] temp;
   work pop 4 {
       if (message_value == 0)
           phase1();
       else
           phase2();
       for (int i = 0; i < 4; i++) {
           println(temp[i]);
       }
   }

   void phase1() pop 4 {
       for (int i = 0; i < 4; i++) {
           temp[i] = pop();
       }
   }

   void phase2() pop 4 {
       for (int i = 0; i < 4; i++) {
           temp[3-i] = pop();
       }
   }

   handler getTheMessage(int val) {
       println("got the message " + val);
       message_value = val;
   }
}
