/** 
 * Really simple fir program for doing frequency replacement testing.
 **/
void->void pipeline LinearTest26 {
   add FloatSource(10);
   add pipeline {
     add Expander(2);
     add AverageFilter(2);
     add Compressor(3);
   };
   add FloatSink();
 }

 void->float stateful filter FloatSource(int N) {
   int idx;
   float[N] x;
   init {
     for (int i=0; i<N; i++) {
       x[i] = i;
     }
     idx = 0;
   }
   work push 1 {
     push(x[idx]);
     idx = (idx + 1) % N;
   }
 }

 float->void filter FloatSink {
   work pop 1 {
     println(pop());
   }
 }

float->float filter AverageFilter(int N) {
  work peek N pop 1 push 1 {
    float sum = 0;
    for (int i=0; i<N; i++) {
      sum += peek(i);
    }
    push(sum/N);
    pop();
  }
}

float->float filter Expander(int L) {
  work pop 1 push L {
    push(pop());
    for (int i=0; i<(L-1); i++) {
      push(0);
    }
  }
}

float->float filter Compressor(int M) {
  work pop M push 1 {
    push(pop());
    for (int i=0; i<(M-1); i++) {
      pop();
    }
  }
}
