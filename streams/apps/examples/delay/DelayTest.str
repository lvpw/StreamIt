void->void pipeline DelayTest {
    add FloatSource();
    add Delay(5);
    add FloatPrinter;
}

/* This filter is a delay element that outputs N zeros before letting
 * the inputs pass through.  It is implemented using a prework
 * function, which should be more efficient than using internal
 * buffering or feedback loops.
 * $Id: DelayTest.str,v 1.2 2006-09-06 05:36:16 thies Exp $ */
float->float filter Delay(int N) {

    // push N zeros at start of execution
    prework push N {
        for (int i=0; i<N; i++) {
            push(0.0);
        }
    }

    // then let inputs pass through
    work push 1 pop 1 {
        push(pop());
    }
}

void->float filter FloatSource() {
    float i = 1.0;
    work push 1 pop 0 {
	push(i);
	i += 1.0;
    }
}

float->void filter FloatPrinter() {
  work pop 1 {
    println(pop());
  }
}