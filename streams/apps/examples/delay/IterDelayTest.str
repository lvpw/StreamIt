void->void pipeline IterDelayTest {
    add IntSource();
    add Delay(5);
    add IntPrinter;
}

/* This filter is a delay element that outputs N zeros before letting
 * the inputs pass through.  It is implemented using a prework
 * function, which should be more efficient than using internal
 * buffering or feedback loops.
 * $Id: DelayTest.str,v 1.3 2006-09-11 12:52:35 thies Exp $ */
int->int filter Delay(int N) {

    // push N zeros at start of execution
    prework push N {
        for (int i=0; i<N; i++) {
            push(0);
        }
    }

    // then let inputs pass through
    work push 1 pop 1 {
        push(pop());
    }
}

void->int stateful filter IntSource() {
    work push 1 pop 0 {
	push(1 + iter());
    }
}

int->void filter IntPrinter() {
  work pop 1 {
    println(pop());
  }
}
