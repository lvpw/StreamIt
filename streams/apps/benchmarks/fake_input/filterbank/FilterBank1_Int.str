/**
 * This is a generic filter bank that decomposes an incoming stream into
 * M frequency bands. It then performs some processing on them (the exact
 * processing is yet to be determined, and then reconstructs them.
 **/
void->void pipeline FilterBank1_Int {
    //    add DataSource();
    //add FileReader<float>("../input/input");
    add FilterBankPipeline(2);
    add FileWriter<int>("FilterBank.out");
}


/** 
 * Total FilterBank structure -- the splitjoin and the final adder.
 **/
void->int pipeline FilterBankPipeline(int M) {
  add FilterBankSplitJoin(M);
  add Adder(M);
}


/**
 * Filterbank splitjoin (everything before the final adder. )
 **/
void->int splitjoin FilterBankSplitJoin(int M) {
    split roundrobin(0);
  for (int i=0; i<M; i++) {
    add ProcessingPipeline(M,i);
  }
  join roundrobin;
}

/** 
 * The main processing pipeline: analysis, downsample, process, upsample, synthesis.
 * I use simple bandpass filters for the Hi(z) and Fi(z).
 **/
void->int pipeline ProcessingPipeline(int M, int i) {
  /* analysis pipeline */
  add pipeline {
    /* take the subband from i*pi/M to (i+1)*pi/M */
      add BandPassFilter(64);
    /* decimate by M */
    add Compressor(M);
  };

  /* process the subband */
  add ProcessFilter(i);
  
  /* synthesis pipeline */
  add pipeline {
    /* upsample by M */
    add Expander(M);
    /* synthesize (eg interpolate) */
    add BandStopFilter(64);
  };
}

/* this is the filter that we are processing the sub bands with. */
int->int filter ProcessFilter(int order) {
  work pop 1 push 1 {
    push(pop());
  }
}

/**
 * A simple adder which takes in N items and
 * pushes out the sum of them.
 **/
int->int filter Adder(int N) {
  work pop N push 1 {
    int sum = 0;
    for (int i=0; i<N; i++) {
      sum += pop();
    }
    push(sum);
  }
}

/* This is a bandpass filter with the rather simple implementation of
 * a low pass filter cascaded with a high pass filter. The relevant parameters
 * are: end of stopband=ws and end of passband=wp, such that 0<=ws<=wp<=pi
 * gain of passband and size of window for both filters. Note that the high 
 * pass and low pass filters currently use a rectangular window.
 **/
void->int pipeline BandPassFilter(int numSamples) {
  add LowPassFilterSource(numSamples);
  add HighPassFilter(numSamples);
}
/* This is a bandstop filter with the rather simple implementation of
 * a low pass filter cascaded with a high pass filter. The relevant parameters
 * are: end of passband=wp and end of stopband=ws, such that 0<=wp<=ws<=pi
 * gain of passband and size of window for both filters. Note that the high 
 * pass and low pass filters currently use a rectangular window.
 *
 * We take the signal, run both the low and high pass filter separately and then
 * add the results back together.
 **/
int->int pipeline BandStopFilter(int numSamples) {
  add splitjoin {
    split duplicate;
    add LowPassFilter(numSamples);
    add HighPassFilter(numSamples);
    join roundrobin;
  };
  /* sum the two outputs together. */
  add Adder(2); 
}
/**
 * This filter compresses the signal at its input by a factor M.
 * Eg it inputs M samples, and only outputs the first sample.
 **/
int->int filter Compressor(int M) {
  work peek M pop M push 1 {
    push(pop());
    for (int i=0; i<(M-1); i++) {
      pop();
    }
  }
}
/**
 * This filter expands the input by a factor L. Eg in takes in one
 * sample and outputs L samples. The first sample is the input
 * and the rest of the samples are zeros. 
 **/
int->int filter Expander(int L) {
  work peek 1 pop 1 push L {
    push(pop());
    for (int i=0; i<(L-1); i++) {
      push(0);
    }
  }
}
    
/**
 * Simple sink that just prints the data that is fed to it.
 **/
int->void filter IntPrinter {
  work pop 1 {
    println(pop());
  }
}
/**
 * Simple StreamIt filter that simply absorbs floating 
 * point numbers without printing them. 
 **/
int->void filter FloatSink {
  work pop 1 {
    pop();
  }
}
/** 
 * Simple FIR high pass filter with gain=g, stopband ws(in radians) and N samples.
 *
 * Eg
 *                 ^ H(e^jw)
 *                 |
 *     --------    |    -------
 *     |      |    |    |     |
 *     |      |    |    |     |
 *    <-------------------------> w
 *                   pi-wc pi pi+wc
 *
 *
 * This implementation is a FIR filter is a rectangularly windowed sinc function 
 * (eg sin(x)/x) multiplied by e^(j*pi*n)=(-1)^n, which is the optimal FIR high pass filter in 
 * mean square error terms.
 *
 * Specifically, h[n] has N samples from n=0 to (N-1)
 * such that h[n] = (-1)^(n-N/2) * sin(cutoffFreq*pi*(n-N/2))/(pi*(n-N/2)).
 * where cutoffFreq is pi-ws
 * and the field h holds h[-n].
 */
int->int filter HighPassFilter(int N) {
  int[N] h;

  /* since the impulse response is symmetric, I don't worry about reversing h[n]. */
  init {
      for (int i=0; i<N; i++) {
	h[i] = i;
    } 
  }

  /* implement the FIR filtering operation as the convolution sum. */
  work peek N pop 1 push 1 {
    int sum = 0;
    for (int i=0; i<N; i++) { 
      sum += h[i]*peek(i);
    }
    push(sum);
    pop();
  }
}

/** 
 * Simple FIR low pass filter with gain=g, wc=cutoffFreq(in radians) and N samples.
 * Eg:
 *                 ^ H(e^jw)
 *                 |
 *          ---------------
 *          |      |      |
 *          |      |      |
 *    <-------------------------> w
 *         -wc            wc
 *
 * This implementation is a FIR filter is a rectangularly windowed sinc function 
 * (eg sin(x)/x), which is the optimal FIR low pass filter in 
 * mean square error terms.
 *
 * Specifically, h[n] has N samples from n=0 to (N-1)
 * such that h[n] = sin(cutoffFreq*pi*(n-N/2))/(pi*(n-N/2)).
 * and the field h holds h[-n].
 */
int->int filter LowPassFilter(int N) {
  int[N] h;

  /* since the impulse response is symmetric, I don't worry about reversing h[n]. */
  init {
    for (int i=0; i<N; i++) {
	h[i] = i;
    }
  }

  /* Implement the FIR filtering operation as the convolution sum. */
  work peek N pop 1 push 1 {
    int sum = 0;
    for (int i=0; i<N; i++) { 
      sum += h[i]*peek(i);
    }
    push(sum);
    pop();
  }
}


/** 
 * Simple FIR low pass filter with gain=g, wc=cutoffFreq(in radians) and N samples.
 * Eg:
 *                 ^ H(e^jw)
 *                 |
 *          ---------------
 *          |      |      |
 *          |      |      |
 *    <-------------------------> w
 *         -wc            wc
 *
 * This implementation is a FIR filter is a rectangularly windowed sinc function 
 * (eg sin(x)/x), which is the optimal FIR low pass filter in 
 * mean square error terms.
 *
 * Specifically, h[n] has N samples from n=0 to (N-1)
 * such that h[n] = sin(cutoffFreq*pi*(n-N/2))/(pi*(n-N/2)).
 * and the field h holds h[-n].
 */
void->int filter LowPassFilterSource(int N) {
  int[N] h;

  int fake_peek(int i) {
      return 5;
  }

  /* since the impulse response is symmetric, I don't worry about reversing h[n]. */
  init {
    for (int i=0; i<N; i++) {
	h[i] = i;
    }
  }

  /* Implement the FIR filtering operation as the convolution sum. */
  work push 1 {
    int sum = 0;
    for (int i=0; i<N; i++) { 
      sum += h[i]*fake_peek(i);
    }
    push(sum);
  }
}
