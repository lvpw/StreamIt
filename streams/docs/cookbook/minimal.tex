\section{A Minimal Program}

\begin{textpic}{\includegraphics{cookbook.1}}
\begin{lstlisting}{}
void->void pipeline Minimal {
  add IntSource;
  add IntPrinter;
}
void->int filter IntSource {
  int x;
  init { x = 0; }
  work push 1 { push(x++); }
}
int->void filter IntPrinter {
  work pop 1 { print(pop()); }
}
\end{lstlisting}
\end{textpic}

This is the minimal interesting StreamIt program.  \lstinline|Minimal|
is a StreamIt \emph{pipeline}: the output of its first child is
connected to the input of its second child, and so on.  It has two
children, a source and a sink.  Each of these are implemented as
StreamIt \emph{filter} objects.

A filter has two special functions, an \emph{init} function and a
\emph{work} function.  Both of these are present in
\lstinline|IntSource|.  The init function runs once at the start of
the program; the work function runs repeatedly forever.  If the init
function is omitted, as it is in \lstinline|IntPrinter|, it is assumed
to be empty.  Work functions have \emph{static data rates}.  The
source here declares that each iteration of the work function pushes a
single item on to its output; the sink declares that it pops a single
item from its input.

Every StreamIt structure has a single input and a single output.  The
filter and pipeline declarations here show the types of these inputs
and outputs.  C-like \lstinline|int| and \lstinline|float| types are
available, along with \lstinline|bit| for one-bit data and
\lstinline|complex| for complex floating-point data.  \lstinline|void|
is used as a special type to indicate the boundary of the program:
``the program'' in StreamIt is defined as a stream structure with both
\lstinline|void| input and output types.  A filter that takes no input
at all should also be declared to take \lstinline|void| as its input
type, and similarly a \lstinline|void| output can be used if a filter
produces no output.

