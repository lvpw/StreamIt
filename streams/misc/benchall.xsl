<?xml version="1.0" ?>
<!--
  benchall.xsl: convert an XML listing of StreamIt benchmarks to HTML
  David Maze &lt;dmaze@cag.lcs.mit.edu&gt;
  $Id: benchall.xsl,v 1.2 2003-09-25 12:42:28 dmaze Exp $

  Notes for the uninitiated: this is an XSL Transform stylesheet.  Use
  an XSLT processor, such as xsltproc, to convert XML to XML using this;
  the input is the file generated by build-bench-xml.py, the output is
  XHTML.

  XSLT stylesheets are structured as a series of templates.  Each template
  is essentially a function call, with local (immutable) variables and
  (immutable) parameters, both with default values; it is called when the
  input XML tag matches the specified expression.  xsl:apply-templates
  calls appropriate templates for children of the current node.  XML tags
  that aren't xsl: tags, and things inside xsl:text, are copied verbatim
  to the output.
-->

<xsl:stylesheet version="1.0"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

  <xsl:template match="/benchset">
    <html>
      <head>
        <title>StreamIt Benchmarks</title>
      </head>
      <body>
        <h1>StreamIt Benchmarks</h1>
        <xsl:apply-templates/>
      </body>
    </html>
  </xsl:template>

  <xsl:template match="dir">
    <xsl:param name="subdir">.</xsl:param>
    <xsl:param name="depth">2</xsl:param>
    <!-- Create an HTML header, labelled with the name attribute of the
         directory node. -->
    <xsl:element name="h{$depth}">
      <xsl:value-of select="@name"/>
    </xsl:element>
    <xsl:apply-templates>
      <xsl:with-param name="subdir">
        <xsl:value-of select="$subdir"/>/<xsl:value-of select="@name"/>
      </xsl:with-param>
      <xsl:with-param name="depth" select="$depth+1"/>
    </xsl:apply-templates>
  </xsl:template>

  <xsl:template match="benchmark">
    <xsl:param name="subdir">.</xsl:param>
    <xsl:param name="depth">3</xsl:param>
    <xsl:element name="h{$depth}">
      <xsl:value-of select="name"/> - <xsl:value-of select="desc"/>      
    </xsl:element>
    <p><xsl:value-of select="description"/></p>
    <xsl:apply-templates select="reference"/>
    <xsl:apply-templates select="implementations">
      <xsl:with-param name="subdir" select="$subdir"/>
    </xsl:apply-templates>
  </xsl:template>

  <xsl:template match="reference">
    <p><b>Reference:</b> <xsl:apply-templates/></p>
  </xsl:template>

  <xsl:template match="implementations">
    <xsl:param name="subdir">.</xsl:param>
    <!-- <p><b>Implementations:</b><br/> -->
    <xsl:apply-templates>
      <xsl:with-param name="subdir" select="$subdir"/>
    </xsl:apply-templates>
    <!-- </p> -->
  </xsl:template>

  <xsl:template match="impl">
    <!-- Save the declared subdirectory, if any. -->
    <xsl:param name="subdir">.</xsl:param>
    <xsl:variable name="path">
      <xsl:value-of select="$subdir"/>
      <xsl:if test="@dir">
        <xsl:text>/</xsl:text><xsl:value-of select="@dir"/>
      </xsl:if>
      <xsl:text>/</xsl:text>
    </xsl:variable>
    <p><b><xsl:value-of select="@lang"/></b>:
    <xsl:if test="desc"><xsl:value-of select="desc"/><br/></xsl:if>
    <xsl:for-each select="file">
      <xsl:element name="a">
        <xsl:attribute name="href">
          <xsl:value-of select="concat($path,.)"/>
        </xsl:attribute>
        <xsl:value-of select="."/>
      </xsl:element>
      <xsl:text> </xsl:text>
    </xsl:for-each>
  </p>
  </xsl:template>
</xsl:stylesheet>
